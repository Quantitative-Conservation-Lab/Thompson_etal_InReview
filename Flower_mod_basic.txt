
model{

# -------------------------------------------------
# States (S):
# 1 empty
# 2 low state
# 3 high state
# 
# Observations (O):  
# 1 not observed
# 2 observed low state
# 3 observed high state
# -------------------------------------------------

#### PRIORS ####
  
  #detection parameters
  
  rho.l ~ dbeta(pl_a,pl_b) #base detection for low state
  alpha.l ~ dnorm(l_mean,l_tau) #difference in baseline detection probability between observation1 and 2, low state
  rho.h ~ dbeta(ph_a,ph_b) #base detection for high state
  alpha.h ~ dnorm(h_mean, h_tau) #difference in baseline detection probability between observation1 and 2, high state
  delta ~ dbeta(delta_a, delta_b) #probability of observing the high state given species has been detected and true state is high

  l_tau <- 1/(l_sd * l_sd) #precision
  h_tau <- 1/(h_sd * h_sd) #precision

  logit(pmulti.l) <- alpha.l + rho.l 
  logit(pmulti.h) <- alpha.h + rho.h
  
  
  #--------------------------------------------------#
  # OCCUPANCY PROBABILITIES
  for (i in 1:n.sites){  
  
    omega[i] ~ dbeta(omega.a[i], omega.b[i]) #probability of being invaded regardless of state
    phi[i] ~ dbeta(phi.a[i], phi.b[i]) #conditional probability that FR is high abundance given present
    
    ps[1,i] <- 1-omega[i] #probability of not invaded
    ps[2,i] <- omega[i]*(1-phi[i]) #probability of invaded at low abundance
    ps[3,i] <- omega[i]*phi[i] #probability of invaded at high abundance
  
    
  #--------------------------------------------------#
  # OBSERVATION PROBABILITIES (for multistate data) 
    # Observation probabilities of given S(t)
    #index = [current state, location, current observation]
   
    #Empty and not observed    
    po_multi[1,i,1] <- 1
    
    #Empty and observed low 
    po_multi[1,i,2] <- 0
    
    #Empty and observed high  
    po_multi[1,i,3] <- 0
 
    #Low state and not observed   
    po_multi[2,i,1] <- 1-pmulti.l #not detected probability
    
    #Low state and observed low 
    po_multi[2,i,2] <- pmulti.l #detection probability
      
    #Low state and observed high 
    po_multi[2,i,3] <- 0 
      
    #High state and not observed   
    po_multi[3,i,1] <- 1-pmulti.h #not detected probability
      
    #High state and observed low 
    po_multi[3,i,2] <- (1-pmulti.h)*(1-delta) #detection probability
      
    #high abundance observed high abundance
    po_multi[3,i,3] <- pmulti.h*delta


  #### LIKELIHOOD ####
    State[i] ~ dcat(ps[,i])
  
    y.multi[i] ~ dcat(po_multi[State[i], i, ])
  
    #Derived parmeter
    State.fin[i] <- State[i] 
   
} #i
  
} #end model

