
model{

# -------------------------------------------------
# States (S):
# 1 empty
# 2 low state
# 3 high state
# 
# Observations (y.single):  
# 1 not detected
# 2 detected 
#
# Observations (y.multi):  
# 1 not detected
# 2 detected low state
# 3 detected high state
# -------------------------------------------------

#--------------------------------------------------#
#### PRIORS ####
  
  #detection parameters
  
  rho.l ~ dbeta(pl.a,pl.b) #base detection for low state 
  alpha.l ~ dnorm(l.mean,l.tau) #difference in low state detection probability between observation datas
  
  rho.h ~ dbeta(ph.a,ph.b) #base detection for high state
  alpha.h ~ dnorm(h.mean,h.tau) #difference in low state detection probability between observation datas

  delta ~ dbeta(delta.a, delta.b) #probability of observing the high state given species has been detected and true state is high


  l.tau <- 1/(l.sd * l.sd) #precision
  h.tau <- 1/(h.sd * h.sd) #precision
  
  logit(psingle.l) <- rho.l - alpha.l
  logit(psingle.h) <- rho.h - alpha.h

  pmulti.l <-  rho.l
  pmulti.h <-  rho.h
  
  #state parameters
  
  for (i in 1:n.sites){  
    omega[i] ~ dbeta(omega.a[i], omega.b[i]) #probability of being invaded regardless of state
    phi[i] ~ dbeta(phi.a[i], phi.b[i]) #conditional probability that FR is high abundance given present
    
    ps[1,i] <- 1-omega[i] #probability of not invaded
    ps[2,i] <- omega[i]*(1-phi[i]) #probability of invaded at low abundance
    ps[3,i] <- omega[i]*phi[i] #probability of invaded at high abundance
  
    
  #--------------------------------------------------#
  # OBSERVATION PROBABILITIES
  #index = [current state, location, current observation]
  ###-------------- single state data --------------###
    #Empty and not observed  
    po.single[1,i,1] <- 1
    
    #Empty and observed
    po.single[1,i,2] <- 0
 
    #Low state and not observed
    po.single[2,i,1] <- 1-psingle.l 
    
    #Low state and observed
    po.single[2,i,2] <- psingle.l 
    
    #High state and not observed
    po.single[3,i,1] <- 1-psingle.h 
    
    #High state and observed
    po.single[3,i,2] <- psingle.h 
  
  ###-------------- multi state data --------------###
    
    #Empty and not detected    
    po.multi[1,i,1] <- 1
    
    #Empty and detected low 
    po.multi[1,i,2] <- 0
    
    #Empty and detected high  
    po.multi[1,i,3] <- 0
 
    #Low state and not detected   
    po.multi[2,i,1] <- 1-pmulti.l 
    
    #Low state and detected low 
    po.multi[2,i,2] <- pmulti.l 
      
    #Low state and detected high 
    po.multi[2,i,3] <- 0 
      
    #High state and not detected   
    po.multi[3,i,1] <- 1-pmulti.h 
      
    #High state and detected low 
    po.multi[3,i,2] <- (1-pmulti.h)*(1-delta) 
      
    #high abundance detected high abundance
    po.multi[3,i,3] <- pmulti.h*delta

  #--------------------------------------------------#
  #### LIKELIHOOD ####
    State[i] ~ dcat(ps[,i])
  
    y.multi[i] ~ dcat(po.multi[State[i], i, ])
  
    y.single[i] ~ dcat(po.single[State[i],i,])
    
    #Derived parmeter
    State.fin[i] <- State[i] 
   
} #i
  
} #end model

