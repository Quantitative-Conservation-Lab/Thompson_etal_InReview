
model{

# -------------------------------------------------
# Parameters:
# gamma: invasion probability
# eps: erradication probability
# p: probability of observing the plant
# -------------------------------------------------
# States (S):
# 1 empty
# 2 invaded
# 
# Observations (O):  
# 1 absent
# 2 present
# -------------------------------------------------

#### PRIORS ####
  eps.l ~ dbeta(eps.l_a,eps.l_b) #erradication when at low state
  eps.h ~ dbeta(eps.h_a,eps.h_b) #erradication when at high state
  
  gamma ~ dbeta(1,1)
  
  phi.lh ~ dbeta(phi.lh_a, phi.lh_b) #transition from low to high
  phi.hh ~ dbeta(phi.hh_a, phi.hh_b) #transition from high to high
  
  rho.l ~ dbeta(pl_a,pl_b) #base detection for low state
  alpha.l ~ dnorm(l_mean,l_tau) #difference in baseline detection probability between observation1 and 2, low state
  rho.h ~ dbeta(ph_a,ph_b) #base detection for high state
  alpha.h ~ dnorm(h_mean, h_tau) #difference in baseline detection probability between observation1 and 2, high state
  delta ~ dbeta(delta_a, delta_b) #probability of observing the high state given species has been detected and true state is high


  #detection parameters
  l_tau <- 1/(l_sd * l_sd) #precision
  h_tau <- 1/(h_sd * h_sd) #precision
  logit(p1.l) <- rho.l
  logit(p1.h) <- rho.h
  
  
  logit(p2.l) <- alpha.l + rho.l 
  logit(p2.h) <- alpha.h + rho.h
  
#--------------------------------------------------#
# STATE TRANSITION
for (i in 1:n.sites){  
  # State transition probabilities: probability of S(t+1) given S(t)
  for (t in 1:n.months){
    #index = [current state, location, time, future state]
    #empty stay empty
    ps[1,i,t,1] <- 1-gamma #1-gamma = not invasion probability
    
    #empty to low abundance
    ps[1,i,t,2] <- gamma #invasion probability
    
    #empty to high abundance
    ps[1,i,t,3] <- 0 #invasion probability

    #low abundance to empty
    ps[2,i,t,1] <- (eps.l*rem.vec[i]) #erradication probability
                                      # rem.vec[i] = 0,1 if 0, then no removal and no erradiction
    
    #low abundance to low abundance
    ps[2,i,t,2] <- (1- eps.l*rem.vec[i])*(1-phi.lh) #erradication failure probability
    
    #low abundance to high abundance
    ps[2,i,t,3] <- (1- eps.l*rem.vec[i])*(phi.lh)
    
    #high abundance to empty
    ps[3,i,t,1] <- (eps.h*rem.vec[i]) #erradication probability
    
    #high abundance to low abundance
    ps[3,i,t,2] <- (1- eps.h*rem.vec[i])*(1-phi.hh) #erradication failure probability
    
    #low abundance to high abundance
    ps[3,i,t,3] <- (1- eps.h*rem.vec[i])*(phi.hh)
    
    #--------------------------------------------------#
    # OBSERVATION PROBABILITIES 1 (for detection/nondetection data)
    
    #Empty and not observed  
    po_dat1[1,i,t,1] <- 1
    
    #Empty and observed
    po_dat1[1,i,t,2] <- 0
 
    #Low state and not observed
    po_dat1[2,i,t,1] <- 1-p1.l #not detected probability
    
    #Low state and observed
    po_dat1[2,i,t,2] <- p1.l #detection probability
    
    #High state and not observed
    po_dat1[3,i,t,1] <- 1-p1.h #not detected probability
    
    #High state and observed
    po_dat1[3,i,t,2] <- p1.h #detection probability
    
    
    #--------------------------------------------------#
    # OBSERVATION PROBABILITIES 2 (for multistate data) 
    # Observation probabilities of given S(t)
    #index = [current state, location, time, current observation]
   
    #Empty and not observed    
    po_dat2[1,i,t,1] <- 1
    
    #Empty and observed low 
    po_dat2[1,i,t,2] <- 0
    
    #Empty and observed high  
    po_dat2[1,i,t,3] <- 0
 
    #Low state and not observed   
    po_dat2[2,i,t,1] <- 1-p2.l #not detected probability
    
    #Low state and observed low 
    po_dat2[2,i,t,2] <- p2.l #detection probability
    
    #Low state and observed high 
    po_dat2[2,i,t,3] <- 0 
    
    #High state and not observed   
    po_dat2[3,i,t,1] <- 1-p2.h #not detected probability
    
    #High state and observed low 
    po_dat2[3,i,t,2] <- (1-p2.h)*(1-delta) #detection probability
    
    #high abundance observed high abundance
    po_dat2[3,i,t,3] <- p2.h*delta

   
  } #t
} #i

  #### Likelihood ####
  for (i in 1:n.sites){
    # Define state at beginning
    State[i,1] <- S.init[i] #we know state at the start
    
    for (t in 2:n.months){ #12
      # State process: state given previous state and transition probability
      State[i,t] ~ dcat(ps[State[i,t-1], i, t-1, ]) 
      
    } #t
      
      #Derived parameter:
      
      State.fin[i] <- State[i,n.months] #state after 12 months
      
  } #i
  
  
  
  for(h in 1:n.obs){
  
    # Observation process: draw observation given state
    y1[site.obs1[h]] ~ dcat(po_dat1[State.fin[site.obs1[h]], site.obs1[h], n.months-1, ])
    
    y2[site.obs2[h]] ~ dcat(po_dat2[State.fin[site.obs2[h]], site.obs2[h], n.months-1, ])
    
    
  } #obs sites
  

} #end model

